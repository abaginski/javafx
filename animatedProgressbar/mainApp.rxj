say .dateTime~new "starting..."

.environment~setEntry("dp", .displayProgress~new(0,100))   -- create the progress object, supply lower and upper bound value
.dp~showGUI          -- start displaying

w =.worker~new(.dp)         -- create the worker, give it the progress object
w~work(1,270)               -- start the work
w~workDone                 -- wait for work done

say .dateTime~new "ended."


::requires "ProgressDefinition.cls"

------------------------------------------------ Sample DisplayProgress class
   -- use multiple inheritance
::class DisplayProgress inherit ProgressDefinition

::method init
  forward class (.ProgressDefinition) continue -- let the ProgressDefinition constructor handle the arguments
  say "lowerBound="self~lowerBound "upperBound=" self~upperBound "currentValue="self~currentValue "currentPosition="self~currentPosition

::method displayProgress
  say self "displayProgress, just arrived, TID="sysqueryprocess("TID")
  say self "displayProgress, continuing work on a new thread, TID="sysqueryprocess("TID")
  oldPosition=-1
  do while self~processingEnded=.false
     if oldPosition<>self~currentPosition then
     do
		newValue = format(self~currentPosition*100,3,2)
		.progressbarManager~update(newValue / 100)
		.progressbarManager~log("Arrived at" newValue "%")
		now = .DateTime~new
		estimatedLength = ((now - .startTime) / newValue) * 100			-- calculate the estimated total duration
		timeLeft = .startTime + estimatedLength - now					-- calculate the remaining time
		if timeLeft~seconds > 10 | .remainingTimeManager~alreadyExceeded60s then
		  .remainingTimeManager~setEstimate(timeLeft)				-- only show the label if the estimated time exceeds a minute or was shown before
        say self~lowerBound".."self~currentValue".."self~upperBound "->" format(self~currentPosition*100,3,2)"%"
        oldPosition=self~currentPosition
     end
     call sysSleep 1    -- sleep a second
  end
  if \self~progressStoppedByUser then do
	   -- show end-of-progress values
	  .progressbarManager~update(1)
	  .remainingTimeManager~hide
	  .progressbarManager~log("Worker done")
	  say self~lowerBound".."self~currentValue".."self~upperBound "->" format(self~currentPosition*100,3,2)"%"
	  say self "displayProcess ending."
  end
  else
	.progressbarManager~log("successfully aborted")
  endTime = .dateTime~new
  .progressbarManager~log("Execution ended at" endTime~normalTime)
  diff = endTime - .startTime
  if diff~seconds > 60 then do
	diffOutput = diff~minutes "minutes"
	if diff~minutes <> 1 then
		diffOutput ||= "s"
  end
  else do
	diffOutput = diff~seconds "second"
	if diff~seconds <> 1 then
		diffOutput ||= "s"
  end
  .progressbarManager~log("Total execution time was" diffOutput)

  
::method showGUI unguarded
  reply								-- return to main flow
  call "animatedProgressbar.rxj"	-- setup and start the javaFX gui

::method GUIstopped
  say "GUI stopped, main app is aware"
  
------------------------------------------------ Sample Worker class: needs access to the displayProgress object

::class Worker
::attribute progressIndicator
::method init
  expose progressIndicator workDone
  use strict arg progressIndicator
  workDone=.false

::method workDone
  expose workDone
  guard on when workDone=.true

::method work
  expose progressIndicator  workDone
  say self "work, just arrived, progressIndicator="progressIndicator", workDone="workDone", TID="sysqueryprocess("TID")
  use strict arg startValue, endValue

  reply                 -- return to caller, proceed in a new thread
  say self "work, continuing on new thread, startValue="startValue", endValue="endValue", TID="sysqueryprocess("TID")
   -- set the lower and upper bound values
  progressIndicator~lowerBound=startValue
  progressIndicator~upperBound=endValue

  do i=startValue to endValue          -- loop
     progressIndicator~currentValue=i  -- set value for progress indicator
     call syssleep 0.13
     if progressIndicator~progressStoppedByUser=.true then
     do
        say .dateTime~new self "progressStoppedByUser=.true, aborting work!"
        workDone=.true
        return
     end
  end
  progressIndicator~processingEnded=.true
  workDone=.true



